---
slug: /comlink/reference/map/binary_data
displayed_sidebar: referenceSidebar
---

# Binary Data

:::info

To use this feature, you will need OneSDK for NodeJS in version `2.2.0` or higher.

:::

To use binary data to upload content OneSDK has `BinaryData` class.

```ts
export interface IBinaryData {
  peek(size?: number): Promise<Buffer | undefined>;
  getAllData(): Promise<Buffer>;
  chunkBy(chunkSize: number): AsyncIterable<Buffer>;
  toStream(): Readable;
}
```

If `BinaryData` instance gets to be used as body or value in `FormData`, the content is streamed. This allows you to upload and work with big files.

Currently `BinaryData` can't be used as result of the use-case. If it is used as result an `UnexpectedError` is returned instead. If the content represented by `BinaryData` should be returned, calling `getAllData()` will read stream into `Buffer`, which is valid use-case result and is returned. Later streamed results support will be added for situation when the content is too big to load into memory.

## Comlink Map examples

In this section you will find example uses of `BinaryData` in Comlink maps.

### Send file in body
```
map SendFile {
  http POST "/resource" {
    request "application/octet-stream" {
      body = input.file
    }

    response 200 "text/plain" {
      map result body
    }
  }
}
```

### Send multiple files as FormData field alongside other fields
```
map SendFile {
  http POST "/resource" {
    request "multipart/form-data" {
      body {
        field = input.field
        file1 = input.file1
        file2 = input.file2
      }
    }

    response 200 "text/plain" {
      map result body
    }
  }
}
```

### Upload file by chunks
```
profile = "example@1.0"
provider = "localhost"

map ChunkedUpload {
  uploadResults = call foreach(chunk of input.file.chunkBy(10)) Upload(chunk = chunk)

  return map result uploadResults
}

operation Upload {
  http POST "/upload/{args.uploadId}" {
    request "application/octet-stream" {
      body = args.chunk
    }

    response 200 "text/plain" {
      return true
    }

    response 500 "text/plain" {
      return false
    }
  }
}
```

### Read first bytes of file and then send it in body
```
map SendFile {
  firstTen = input.file.peek(10)

  contentType = firstTen.toString('ascii').toLowerCase().includes('pdf') ? 'application/pdf' : 'application/octet-stream'

  http POST "/resource" {
    request {
      headers {
        'content-type' = contentType
      }
      
      body = input.file

    response 200 "text/plain" {
      map result body
    }
  }
}
```

### Update BinaryData from Comlink Map
```
map Example {
  tmp = (input.file.name = input.filename)
  tmp = (input.file.mimetype = input.mimetype)
  
  http POST "/" {
    request "multipart/form-data" {
      body = {
        file: input.file
      }
    }
  
    response 200 {
      map result body
    }
  }
}
```

## OneSDK use example

```typescript
import { SuperfaceClient, BinaryData } from '@superfaceai/one-sdk';

const client = new SuperfaceClient();
const profile = await client.getProfile('example');

// for files
const result = await profile.getUseCase('Example').perform({
  binary: BinaryData.fromPath(pathToFile, { mimetype: 'text/plain', filename: 'example.txt' });

// or for Readable streams
const stream = getReadableStream();
const result = await profile.getUseCase('Example').perform({
  binary: BinaryData.fromStream(stream);
});
```

## Limitations

### 1. Asynchronous operations on BinaryData

Current limitation for use in Comlink Map is that `peek`, `read` and `getAllData` methods are asynchronous. So, they can't be directly chained with function on [Buffer](https://nodejs.org/dist/latest-v18.x/docs/api/buffer.html#class-buffer). Most likely `toString()`.

To overcome this limitation, It must be first [set into variable](https://spec.superface.dev/latest/map-spec.html#sec-Set-Variables), so the interpreter resolves the Promise:
```
map Example {
  firstBytes = input.binary.peek(10)
  data = input.binary.read(10)
  remainingData = input.binary.getAllData()

  map result {
    firstBytes = firstBytes
    data = data
   remainingData = remainingData
  }
}
```

### 2. Opaque data type

In profile, the type must be omitted for now. Later[new Primitive](https://spec.superface.dev/latest/profile-spec.html#sec-Primitive-types) representing binary data will be added to Comlink Profile.

```
name = "example"
version = "1.0.0"

usecase Example {
  input {
    binary
  }
}
```

### 3. Assigning to temp variable when altering BinaryData

Comlink maps right now doesn't support Script expression as statement. Onlt workaround for now is to use variable assignement.

```
tmp = (input.file.name = input.filename)
```
